ext {
    propertyString = { key ->
        return project.hasProperty(key) ? project.property(key) : ''
    }
    propertyStringList = { key, delim = ' ' ->
        return project.hasProperty(key) ? project.property(key).split(delim) : []
    }
    propertyBool = { key ->
        return project.hasProperty(key) ? project.property(key).toBoolean() : false
    }
    assertProperty = { key ->
        if (!project.hasProperty(key))
            throw new GradleException("Required property '$key' does not exist! Please add it to gradle.properties.")
    }
    assertSubProperties = { key, props ->
        if (propertyBool(key)) {
            for (def prop : props) {
                assertProperty(prop)
            }
        }
    }
    setDefaultProperty = { key, defaultVal, isString ->
        if (!project.hasProperty(key)) {
            if (isString) {
                project.ext."$key" = "\"$defaultVal\""
            } else {
                project.ext."$key" = defaultVal
            }
        }
    }
}
